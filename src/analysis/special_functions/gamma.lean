import analysis.special_functions.exponential
import measure_theory.integral.integral_eq_improper
import measure_theory.integral.limit_comparison
import analysis.special_functions.integrals


noncomputable theory
open finset filter metric interval_integral set function
open_locale classical topological_space ennreal filter

namespace real


/- The integrand in the integration-by-parts argument -/

def F (s x : ‚Ñù) : ‚Ñù := exp(-x) * x^s

lemma cont_F (s : ‚Ñù) (h1: 0 ‚â§ s) :
  continuous_on s.F (Ici 0) :=
begin
  apply continuous_on.mul,
  { apply continuous_on.exp,
    apply continuous_on.neg,
    apply continuous_on_id },
  { apply continuous_on.rpow_const,
    apply continuous_on_id,
    tauto }
end

lemma deriv_F (s x: ‚Ñù) (h1: 1 ‚â§ s) (h2: 0 ‚â§ x) : has_deriv_at s.F
(- (exp (-x) * x ^ s) + exp (-x) * (s * x ^ (s - 1))) x :=
begin
  have d1 : has_deriv_at (Œª (y: ‚Ñù), exp(-y)) (-exp(-x)) x,
  { simpa using has_deriv_at.exp (has_deriv_at_neg x) },
  have d2 : has_deriv_at (Œª (y: ‚Ñù), y^s) (s*x^(s-1)) x,
  { apply has_deriv_at_rpow_const,
    right,
    exact h1 },
  simpa using has_deriv_at.mul d1 d2
end


/- A long and fiddly argument to show that F decays exponentially at +‚àû -/

/- This lemma should really go in specific_limits.lean or something -/
lemma tendsto_exp_div_rpow_at_top (s : ‚Ñù) : tendsto (Œª x : ‚Ñù, exp x / x ^ s ) at_top at_top :=
begin
  have := archimedean_iff_nat_lt.1 (real.archimedean) s,
  cases this with n hn,
  have t := tendsto_exp_div_pow_at_top n,
  have : 0 < (n:‚Ñù) - s := by linarith,
  replace t := tendsto.at_top_mul_at_top t (tendsto_rpow_at_top this),

  let f1 := (Œª x:‚Ñù, (exp x / x ^ n) * (x ^ (‚Üën - s))),
  let f2 := (Œª x:‚Ñù, exp x / x ^ s),
  have ff1: f1 = (Œª x:‚Ñù, (exp x / x ^ n) * (x ^ (‚Üën - s))) := by refl,
  have ff2: f2 = (Œª x:‚Ñù, exp x / x ^ s) := by refl,

  have : ‚àÄ x : ‚Ñù, (0 < x) ‚Üí f1 x = f2 x,
  --have : ‚àÄ x : ‚Ñù, (0 < x) ‚Üí ( (exp x / x ^ n) * (x ^ (‚Üën - s)) = exp x / x ^ s),
  --have : ‚àÄ x : ‚Ñù, (0 < x) ‚Üí (Œª x:‚Ñù, (exp x / x ^ n) * (x ^ (‚Üën - s))) x = (Œª x:‚Ñù, exp x / x ^ s) x,
  {
    intros x h,
    rw [ff1,ff2],
    ring_nf,
    --rw div_eq_mul_inv (exp x) (x^n),
    rw mul_eq_mul_right_iff,
    left,
    rw [sub_eq_neg_add,rpow_add_nat, mul_assoc],
    have : x^n * (x^n)‚Åª¬π = 1,
    apply mul_inv_cancel,
    apply pow_ne_zero,
    exact h.ne',
    rw [this, mul_one],
    apply rpow_neg,
    exact le_of_lt(h),
    exact h.ne',
  },

  have Icieq: eq_on f1 f2 (Ici 1),
  {
    intros x hx,
    rw [set.Ici, mem_set_of_eq] at hx,
    have b: 0 < x := by linarith,
    exact (this x b),
  },

  have eveq : eventually_eq at_top f1 f2,
  {
    rw eventually_eq_iff_exists_mem,
    use (Ici 1),
    split,
    apply mem_at_top,
    exact Icieq,
  },
  apply tendsto.congr' eveq,
  exact t,
end

/- More general version allowing exp(-bx) for any b > 0 -/
lemma tendsto_exp_mul_div_rpow_at_top (s : ‚Ñù) (b : ‚Ñù) (hb : 0 < b):
  tendsto (Œª x : ‚Ñù, exp (b * x) / x ^ s ) at_top at_top :=
begin
  have t1 := tendsto_exp_div_rpow_at_top (s/b),
  have t2 := tendsto_rpow_at_top hb,
  have t := tendsto.comp t2 t1,

  let f1 := (Œª (x : ‚Ñù), (exp x / x ^ (s / b)) ^ b),
  let f2 := (Œª x : ‚Ñù, exp (b * x) / x ^ s ),
  have ff1 : ‚àÄ x:‚Ñù, f1 x = (exp x / x ^ (s / b)) ^ b,
  {
    by simp only [eq_self_iff_true, forall_const],
  },
  have ff2 : ‚àÄ x:‚Ñù, f2 x = exp (b * x) / x ^ s,
  {
    by simp only [eq_self_iff_true, forall_const],
  },

  have Ioieq: eq_on f1 f2 (Ioi 0),
  {
    intros x hx,
    rw [set.Ioi, mem_set_of_eq] at hx,
    rw [ff1, ff2],
    rw div_rpow,
    -- clean up the subgoals introduced by div_rpow
    show 0 ‚â§ exp x,
    { apply le_of_lt(exp_pos x) },
    show 0 ‚â§ x ^ (s / b),
    { apply le_of_lt,
      apply rpow_pos_of_pos hx },

    have : exp x ^ b = exp (b * x),
    { calc exp x ^ b = exp(log (exp x ^ b ) ) : by { rw exp_log, apply rpow_pos_of_pos (exp_pos x) }
      ...            = exp( b * log (exp x) ) : by rw log_rpow (exp_pos x)
      ...            = exp( b * x )           : by rw log_exp },
    rw this,
    rw div_eq_div_iff,
    show x^s ‚â† 0,
    { symmetry, apply ne_of_lt,
      apply rpow_pos_of_pos,
      linarith },
    show (x ^ (s / b)) ^ b ‚â† 0,
    { symmetry, apply ne_of_lt,
      apply rpow_pos_of_pos,
      apply rpow_pos_of_pos,
      linarith },
    rw mul_eq_mul_left_iff,
    left,
    rw ‚Üêrpow_mul,
    show 0 ‚â§ x, linarith,
    rw div_mul_cancel, exact hb.ne'
  },
  have : Ici (1:‚Ñù) ‚äÜ Ioi (0:‚Ñù),
  {
    rw [set.Ioi, set.Ici],
    intro x,
    rw mem_set_of_eq,rw mem_set_of_eq,
    intro hx,linarith,
  },
  have Ioi_at_top: Ioi (0:‚Ñù) ‚àà at_top := Ioi_mem_at_top (0:‚Ñù),
  have ev_eq: eventually_eq at_top f1 f2 := eventually_eq_of_mem Ioi_at_top Ioieq,

  exact tendsto.congr' ev_eq t
end

lemma tendsto_exp_mul_div_rpow_at_top' (s : ‚Ñù) (b : ‚Ñù) (hb : 0 < b):
  tendsto (Œª x : ‚Ñù, x^s * exp (-b * x)) at_top (ùìù $ (0:‚Ñù)) :=
begin
  have l := tendsto_exp_mul_div_rpow_at_top s b hb,
  have:  (Œª x : ‚Ñù, x^s * exp (-b * x)) =  (Œª x : ‚Ñù, exp (b * x) / x^s)‚Åª¬π,
  {
    ext,
    simp only [neg_mul, pi.inv_apply],
    rw [inv_div,div_eq_mul_inv],
    rw mul_eq_mul_left_iff,
    left,
    apply exp_neg,
  },
  rw this,
  exact tendsto.inv_tendsto_at_top l
end

/- Now we have the bits we need -/
lemma asymp_F (s : ‚Ñù) :
  asymptotics.is_O s.F (Œª x : ‚Ñù, exp(-(1/2) * x)) filter.at_top :=
begin
  apply asymptotics.is_o.is_O,
  apply asymptotics.is_o_of_tendsto,
  { intros x hx,
    exfalso,
    apply ( exp_pos(-(1/2) * x)).ne',
    exact hx },

  simp only [F],

  have : ‚àÄ (x: ‚Ñù), (x > 0) ‚Üí (exp (-x) * x ^ s / exp (-(1 / 2) * x) = exp (-(1/2) * x) * x ^ s),
  {
    intros x h,
    rw mul_comm,
    rw mul_comm (exp (-(1/2) * x)) (x ^ s),
    rw div_eq_of_eq_mul,
    exact (exp_pos (-(1/2) * x)).ne',
    have: exp(-x) = exp(-(1/2)*x) * exp (-(1 / 2) * x),
    {
      rw ‚Üêreal.exp_add,
      simp only [exp_eq_exp],
      ring,
    },
    rw this,
    ring
  },
  replace : eventually_eq at_top
    (Œª x:‚Ñù,(exp (-x) * x ^ s / exp (-(1 / 2) * x))) (Œª x:‚Ñù,  exp (-(1/2) * x) * x ^ s),
  {
    apply eventually_eq_of_mem (Ioi_mem_at_top(0:‚Ñù)),
    intros x hx,
    rw [set.Ioi, mem_set_of_eq] at hx,
    exact (this x hx)
  },
  rw (tendsto_congr' this),
  have : (Œª (x : ‚Ñù), exp (-(1 / 2) * x) * x ^ s) = (Œª (x : ‚Ñù), exp ((1 / 2) * x) / x ^ s)‚Åª¬π,
  {
    ext1,
    simp only [neg_mul, pi.inv_apply],
    rw inv_div,
    rw exp_neg,
    ring,
  },
  rw this,
  apply tendsto.inv_tendsto_at_top,
  exact (tendsto_exp_mul_div_rpow_at_top s (1/2))(one_half_pos) -- hooray!
end





def real_incomplete_gamma (s X : ‚Ñù) : ‚Ñù := ‚à´ x in 0..X, exp(-x) * x^(s-1)

lemma gamma_FE_incomp (s X : ‚Ñù) (h: 1 ‚â§ s) (h2: 0 ‚â§ X):
  real_incomplete_gamma (s+1) X = s * real_incomplete_gamma s X - X^s * exp(-X) :=
begin
  rw real_incomplete_gamma,
  rw real_incomplete_gamma,

  have F_der_I: (‚àÄ (x:‚Ñù), (x ‚àà interval 0 X) ‚Üí
    has_deriv_at s.F (- (exp (-x) * x ^ s) + exp (-x) * (s * x ^ (s - 1))) x),
  {
    intros x hx,
    cases hx,
    rw min_def at hx_left,
    split_ifs at hx_left,
    exact deriv_F s x h hx_left,
    tauto,
  },

  have c : continuous_on (Œª (x : ‚Ñù), exp (-x)) (interval 0 X),
  { apply continuous_on.exp,
    apply continuous_on.neg,
    apply continuous_on_id },

  have dF_int_I: interval_integrable (Œª (x : ‚Ñù),
    -(exp (-x) * x ^ s) + exp (-x) * (s * x ^ (s - 1)))
    measure_theory.measure_space.volume 0 X,
  {
    -- This is an awful mess,
    -- proving continuity of a function
    -- built up as a sum of many terms.
    apply continuous_on.interval_integrable,
    apply continuous_on.add,
    apply continuous_on.neg,
    apply continuous_on.mul,
    exact c,
    apply continuous_on.rpow_const,
    apply continuous_at.continuous_on,
    intros x hxX,
    apply continuous_at_id,
    intros x hxX,
    right,
    exact le_trans(zero_le_one)(h),

    -- halfway...
    apply continuous_on.mul,
    exact c,
    apply continuous_on.mul,
    apply continuous.continuous_on,
    apply continuous_const,
    apply continuous_on.rpow_const,
    apply continuous_at.continuous_on,
    intros x hxX,
    apply continuous_at_id,
    intros x hxX,
    right,
    rw le_sub,
    simp,exact h
  },
  have int_eval := integral_eq_sub_of_has_deriv_at F_der_I dF_int_I,
  clear F_der_I dF_int_I c,
  have : (F s 0) = 0,
  { rw F, rw zero_rpow, ring, apply ne_of_gt,
    apply lt_of_lt_of_le zero_lt_one h },
  rw [this, F] at int_eval,
  simp only [sub_zero] at int_eval,
  rw integral_add at int_eval,
  simp only [add_tsub_cancel_right],
  have : (‚à´ (x : ‚Ñù) in 0..X, exp (-x) * x ^ s)
   = (‚à´ (x : ‚Ñù) in 0..X, exp (-x) * (s * x ^ (s - 1))) - exp (-X) * X ^ s,
  rw sub_eq_neg_add,
  apply eq_add_of_add_neg_eq,
  rw ‚Üê int_eval, simp,ring,
  rw this,
  have : (exp (-X) * X ^ s) = (X^s * exp(-X)) := by ring,
  rw this, simp,
  clear this, clear this,clear this,clear int_eval,
  have : (Œª (x : ‚Ñù), exp (-x) * (s * x ^ (s - 1)))
    = (Œª (x : ‚Ñù), s * (exp (-x) * x ^ (s - 1))),
  ext1, ring,
  rw this,
  apply integral_const_mul,
  clear this, clear int_eval,

  -- now two more integrability statements, yawn
  apply continuous_on.interval_integrable,
  apply continuous_on.neg,
  apply continuous_on.mul,
  apply continuous_on.exp,
  apply continuous_on.neg,
  apply continuous_on_id,
  apply continuous_on.rpow_const,
  apply continuous_on_id,
  intros x hx,
  right,
  exact le_trans(zero_le_one)(h),

  -- and the last one
  apply continuous_on.interval_integrable,
  apply continuous_on.mul,
  apply continuous_on.exp,
  apply continuous_on.neg,
  apply continuous_on_id,
  apply continuous_on.mul,
  apply continuous_on_const,
  apply continuous_on.rpow_const,
  apply continuous_on_id,
  intros x hx,
  right,
  rw le_sub,
  simp only [sub_zero],
  exact h
end


lemma integrable_F (s: ‚Ñù) (h: 1 ‚â§ s): measure_theory.integrable_on
  (Œª (x:‚Ñù), exp(-x) * x^(s-1)) (Ioi 0) :=
begin
  apply limit_comparison.integrable_bigoh_exp (s-1).F 0 (1/2) one_half_pos,
  apply cont_F,
  { linarith },
  exact asymp_F (s-1)
end

def real_gamma (s: ‚Ñù) : ‚Ñù :=  ‚à´ x in (Ioi 0), exp(-x) * x^(s-1)

lemma tendsto_incomplete_gamma (s : ‚Ñù) (h: 1 ‚â§ s):
  tendsto (s.real_incomplete_gamma) (filter.at_top)  (ùìù $ real_gamma s) :=
begin
  apply measure_theory.interval_integral_tendsto_integral_Ioi,
  swap, apply tendsto_id,
  exact integrable_F s h
end

lemma FE_gamma (s : ‚Ñù) (h: 1 ‚â§ s) :
  real_gamma (s+1) = s * real_gamma s :=
begin
  have t1: tendsto (s+1).real_incomplete_gamma at_top (ùìù (s+1).real_gamma),
  { apply tendsto_incomplete_gamma, linarith },
  suffices t2: tendsto (s+1).real_incomplete_gamma at_top (ùìù $ s * real_gamma s),
  { apply tendsto_nhds_unique t1 t2 },

  have a: eventually_eq at_top ((s+1).real_incomplete_gamma)
    (Œª X:‚Ñù, s * real_incomplete_gamma s X - X^s * exp(-X)),
  {
    apply eventually_eq_of_mem (Ici_mem_at_top (0:‚Ñù)),
    intros X hX,
    rw [set.Ici, mem_set_of_eq] at hX,
    exact gamma_FE_incomp s X h hX
  },
  replace a := eventually_eq.symm a,

  suffices b: tendsto (Œª X:‚Ñù, s * real_incomplete_gamma s X - X^s * exp(-X)) at_top
    (ùìù $ s * real_gamma s),
  { exact tendsto.congr' a b, },

  have l1: tendsto (Œª X:‚Ñù, s * real_incomplete_gamma s X) at_top (ùìù $ s * real_gamma s),
  {
    apply tendsto.const_mul,
    exact tendsto_incomplete_gamma s h
  },
  suffices l2: tendsto (Œª X:‚Ñù, -X^s * exp(-X)) at_top (ùìù $ (0:‚Ñù)),
  {
    have := tendsto.add l1 l2,
    simpa using this
  },
  have l3: tendsto (Œª X:‚Ñù, X^s * exp(-X)) at_top (ùìù $ (0:‚Ñù)),
  {
    have := tendsto_exp_mul_div_rpow_at_top' s (1:‚Ñù) zero_lt_one,
    simpa using this
  },
  have: (Œª X:‚Ñù, -X^s * exp(-X)) = (Œª X:‚Ñù, (-1) * (X^s * exp(-X))) := by { simp only [neg_mul, one_mul], },
  rw this,
  have : (0:‚Ñù) = (-1) * (0:‚Ñù) := by {ring, },
  rw this,
  apply tendsto.const_mul,
  exact l3
end

lemma incomp_gamma_at_one (X : ‚Ñù) (hX: 0 < X): real_incomplete_gamma 1 X = 1-exp(-X) :=
begin
  rw real_incomplete_gamma,
  simp
end

lemma gamma_at_one: real_gamma 1 = 1 :=
begin
  have t1: tendsto (1:‚Ñù).real_incomplete_gamma at_top (ùìù (1:‚Ñù).real_gamma),
  {
    apply tendsto_incomplete_gamma,
    refl
  },
  have t2: tendsto (1:‚Ñù).real_incomplete_gamma at_top (ùìù (1:‚Ñù)),
  {
    have t2a: eventually_eq at_top (Œª X:‚Ñù, 1-exp(-X)) (1:‚Ñù).real_incomplete_gamma,
    {
      apply eventually_eq_of_mem (Ioi_mem_at_top (0:‚Ñù)),
      intros X hX,
      symmetry,
      apply incomp_gamma_at_one,
      rw [‚ÜêIoi_def, mem_set_of_eq] at hX, exact hX
    },
    apply tendsto.congr' t2a,

    have t2b: tendsto (Œª X, exp(-X)) at_top (ùìù (0:‚Ñù)),
    {
      have := tendsto_exp_mul_div_rpow_at_top' 0 1,
      simpa using this
    },
    have := tendsto.const_sub (1:‚Ñù) t2b,
    simpa using this
  },
  apply tendsto_nhds_unique t1 t2
end

lemma gamma_integer: ‚àÄ n:‚Ñï, real_gamma (n+1) = nat.factorial n :=
begin
  intro n,
  induction n with n hn,

  simp only [nat.cast_zero, zero_add, nat.factorial_zero, nat.cast_one],
  exact gamma_at_one,

  rw FE_gamma,
  simp only [nat.cast_succ, nat.factorial_succ, nat.cast_mul, mul_eq_mul_left_iff],
  left, exact hn,

  simp only [nat.cast_succ, le_add_iff_nonneg_left, nat.cast_nonneg]
end

end real
